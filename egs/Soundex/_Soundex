Al =	{a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r,s,t,u,v,w,x,y,z};
CAl =	Al $ (0,0);
Di =	{0,1,2,3,4,5,6,7};
CDi =	Di $ (0,0);

So =	( Al $ (0,0 0) ) CAl*
        @ CAl { ({b,f,p,v},         1),
	        ({c,g,j,k,q,s,x,z}, 2),
	        ({d,t},             3),
		({l},               4),
		({m,n},             5),
		({r},               6),
		({a,e,h,i,o,u,w,y}, 7) }*
	@ CAl ( (0+,0) % (1+,1) % (2+,2) % (3+,3) %
	        (4+,4) % (5+,5) % (6+,6) % (7+,7) )
	@ CAl (Di,^) CDi*
	@ CAl ( ( {0,1,2,3,4,5,6} $ (0,0) ) | (7,^) )*
	@ CAl CDi* (^,'000')
	@ CAl (CDi :3) (Di,^)*;

BSo =	{ ({A,a},a), ({B,b},b), ({C,c},c), ({D,d},d), ({E,e},e),
	  ({F,f},f), ({G,g},g), ({H,h},h), ({I,i},i), ({J,j},j),
	  ({K,k},k), ({L,l},l), ({M,m},m), ({N,n},n), ({O,o},o),
	  ({P,p},p), ({Q,q},q), ({R,r},r), ({S,s},s), ({T,t},t),
	  ({U,u},u), ({V,v},v), ({W,w},w), ({X,x},x), ({Y,y},y),
	  ({Z,z},z), ({0,1,2,3,4,5,6,7,8,9,'.','&',''''},a) }* @ So;

TSo = ( BSo ('\n','\n') )*;
TSo :sseq :save TSoundex;

:free;
:time;
